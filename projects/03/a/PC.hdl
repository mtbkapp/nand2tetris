// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    /* inc */
    Inc16(in=rOut, out=plusOne);
    Mux16(a=rOut, b=plusOne, sel=inc, out=rIn0);

    /* load */ 
    Mux16(a=rIn0, b=in, sel=load, out=rIn1);

    /* reset */
    Mux16(a=rIn1, b=false, sel=reset, out=rIn);

    /* set load pin = reset || load || inc */
    Or(a=reset, b=load, out=rLoad0);
    Or(a=rLoad0, b=inc, out=rLoad);
    
    Register(in=rIn, load=rLoad, out=out, out=rOut);
}
